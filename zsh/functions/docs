#autoload

# Quickly launch the testing server for the wiki

(( ${+commands[hugo]} )) || {
	print "error - please install hugo." >&2
	return 1
}

local \
	DOCS=~/Developer/docs \
	PORT="1313" \
	HOST="[::1]" \
	SCHEME="http" \

local AUTHORITY="${HOST}:${PORT}"
local URL="${SCHEME}://${AUTHORITY}/${1:-}"

# Clear any existing traffic on this port coming from `hugo`
local same_port=($(sudo lsof -ti :${PORT}))
local same_name=($(pgrep 'hugo'))

kill ${same_port:*same_name} 2> /dev/null

pushd ${DOCS} > /dev/null

hugo

# Immediately disown the server program
&> /dev/null hugo server \
	--baseURL ${URL} \
	--bind '::1' \
	--port ${PORT} \
	--gc \
	--navigateToChanged \
	--cleanDestinationDir \
	--noHTTPCache \
	--buildFuture \
	--buildDrafts \
	--buildExpired \
	&!

# Wait until we successfully get a response from the page before launching
while (( $(curl ${URL} -- &> /dev/null)$? == 7 )) {
	sleep 0.1
}

local \
	firefox="/Applications/Firefox.app" \
	chrome="/Applications/Google Chrome.app" \
	safari="/Applications/Safari.app" \

if [[ -d ${firefox} ]]; then
	open -a ${firefox} ${URL}
elif [[ -d ${chrome} ]]; then
	open -a ${chrome} ${URL}
elif [[ -d ${safari} ]]; then
	open -a ${safari} ${URL}
else
	open ${URL}
fi

popd > /dev/null

return 0
